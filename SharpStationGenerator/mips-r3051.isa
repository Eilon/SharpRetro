(defm u1  (v) (cast v u1))
(defm u8  (v) (cast v u8))
(defm u16 (v) (cast v u16))
(defm u32 (v) (cast v u32))

(defm i8  (v) (cast v i8))
(defm i16 (v) (cast v i16))
(defm i32 (v) (cast v i32))

(defm check-overflow-add (lhs rhs)
    (block))

(defm set-link () (= (reg 31) (+ (pc) 8)))
(defm branch-with-link (target) (block (set-link) (branch target)))
(defm branch-default () (branch (+ (pc) 8)))

(defm branch-if (cond target)
    (if cond
        (branch target)
        (branch-default)))

(defm check-load-alignment (addr type)
    (when (!= 0 (& addr (- (/ (bitwidth type) 8) 1)))
        (exception ADEL)))

(defm check-store-alignment (addr type)
    (when (!= 0 (& addr (- (/ (bitwidth type) 8) 1)))
        (exception ADES)))

(defm itype (name op dasm decode eval)
    (block
        (assert (== (string-length op) 6) "Expected 6 opcode bits for instruction '$(as-string name)', got $(string-length op)")
        (def name
            "$op sssss ttttt iiiiiiiiiiiiiiii"
            dasm
            (names (rs s) (rt t) (imm i))
            decode
            eval)))
(defm itype (name op dasm eval) (itype name op dasm (block) eval))

(defm ritype (name op funct dasm decode eval)
    (block
        (assert (== (string-length op) 6) "Expected 6 opcode bits for instruction '$(as-string name)', got $(string-length op)")
        (assert (== (string-length funct) 5) "Expected 5 funct bits for instruction '$(as-string name)', got $(string-length op)")
        (def name
            "$op sssss $funct iiiiiiiiiiiiiiii"
            dasm
            (names (rs s) (imm i))
            decode
            eval)))
(defm ritype (name op funct dasm eval) (ritype name op funct dasm (block) eval))

(defm jtype (name op dasm decode eval)
    (block
        (assert (== (string-length op) 6) "Expected 6 opcode bits for instruction '$(as-string name)', got $(string-length op)")
        (def name
            "$op iiiiiiiiiiiiiiiiiiiiiiiiii"
            dasm
            (names (imm i))
            decode
            eval)))
(defm jtype (name op dasm eval) (jtype name op dasm (block) eval))

(defm rtype (name funct dasm decode eval)
    (block
        (assert (== (string-length funct) 6) "Expected 6 funct bits for instruction '$(as-string name)', got $(string-length op)")
        (def name
            "000000 sssss ttttt ddddd aaaaa $funct"
            dasm
            (names (rs s) (rt t) (rd d) (shamt a))
            decode
            eval)))
(defm rtype (name funct dasm eval) (rtype name funct dasm (block) eval))

(defm stype (name funct dasm decode eval)
    (block
        (assert (== (string-length funct) 6) "Expected 6 funct bits for instruction '$(as-string name)', got $(string-length op)")
        (def name
            "000000 cccccccccccccccccccc $funct"
            dasm
            (names (code c))
            decode
            eval)))
(defm stype (name funct dasm eval) (stype name funct dasm (block) eval))

(defm cftype (name op funct dasm decode eval)
    (block
        (assert (== (string-length op) 4) "Expected 4 opcode bits for instruction '$(as-string name)', got $(string-length op)")
        (assert (== (string-length funct) 5) "Expected 5 funct bits for instruction '$(as-string name)', got $(string-length op)")
        (def name
            "$op cc $funct ttttt ddddd fffffffffff"
            dasm
            (names (cop c) (rt t) (rd d) (cofun f))
            decode
            eval)))
(defm cftype (name op funct dasm eval) (cftype name op funct dasm (block) eval))

(rtype ADD "100000" "add %$rd, %$rs, %$rt"
    (mlet (lhs (reg rs)
           rhs (reg rt))
        (check-overflow-add lhs rhs)
        (= (reg rd) (+ lhs rhs))))

(itype ADDI "001000" "addi %$rt, %$rs, $(hex eimm)"
    (let eimm (signext imm u32))
    (let lhs (reg rs)
        (check-overflow-add lhs eimm)
        (= (reg rt) (+ lhs eimm))))

(itype ADDIU "001001" "addiu %$rt, %$rs, $(hex eimm)"
    (let eimm (signext imm u32))
    (= (reg rt) (+ (reg rs) eimm)))

(rtype ADDU "100001" "addu %$rd, %$rs, %$rt"
    (= (reg rd) (+ (reg rs) (reg rt))))

(rtype AND "100100" "and %$rd, %$rs, %$rt"
    (= (reg rd) (& (reg rs) (reg rt))))

(itype ANDI "001100" "andi %$rt, %$rs, $(hex eimm)"
    (let eimm (signext imm u32))
    (= (reg rt) (& (reg rs) eimm)))

(itype BEQ "000100" "beq %$rs, %$rt, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (branch-if (== (reg rs) (reg rt)) target))

(ritype BGEZ "000001" "0###1" "bgez %$rs, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (branch-if (>= (i32 (reg rs)) 0) target))

(ritype BGEZAL "000001" "1###1" "bgezal %$rs, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (block
        (set-link)
        (branch-if (>= (i32 (reg rs)) 0) target)))

(ritype BGTZ "000111" "00000" "bgtz %$rs, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (branch-if (> (i32 (reg rs)) 0) target))

(ritype BLEZ "000110" "00000" "blez %$rs, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (branch-if (<= (i32 (reg rs)) 0) target))

(ritype BLTZ "000001" "0###0" "bltz %$rs, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (branch-if (< (i32 (reg rs)) 0) target))

(ritype BLTZAL "000001" "1###0" "bltzal %$rs, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (block
        (set-link)
        (branch-if (< (i32 (reg rs)) 0) target)))

(itype BNE "000101" "bne %$rs, %$rt, $(hex target)"
    (let target (+ (pcd) (<< (signext imm u32) 2)))
    (branch-if (!= (reg rs) (reg rt)) target))

(stype BREAK "001101" "break $code"
    (exception Break))

(cftype CFC "0100" "00010" "cfc$cop %$rt, $rd"
    (= (reg rt) (copcreg cop rd)))

(def COP
    "0100oo1 ccccccccccccccccccccccccc"
    "cop$cop $(hex command)"
    (names (cop o) (command c))
    (block)
    (copfun cop command))

(cftype CTC "0100" "00110" "ctc$cop %$rt, $rd"
    (= (copcreg cop rd) (reg rt)))

(rtype DIV "011010" "div %$rs, %$rt"
    (mlet (rsv (reg rs)
           rtv (reg rt))
        (if (== rtv 0)
            (block
                (= (reg-lo) (if (!= (& rsv 0x80000000) 0) 1 0xFFFFFFFF))
                (= (reg-hi) rsv))
            (if (& (== rsv 0x80000000) (== rtv 0xFFFFFFFF))
                (block
                    (= (reg-lo) 0x80000000)
                    (= (reg-hi) 0))
                (block
                    (= (reg-lo) (/ (i32 rsv) (i32 rtv)))
                    (= (reg-hi) (% (i32 rsv) (i32 rtv))))))))

(rtype DIVU "011011" "divu %$rs, %$rt"
    (mlet (rsv (reg rs)
           rtv (reg rt))
        (if (== rtv 0)
            (block
                (= (reg-lo) 0xFFFFFFFF)
                (= (reg-hi) rsv))
            (block
                (= (reg-lo) (/ rsv rtv))
                (= (reg-hi) (% rsv rtv))))))

(jtype J "000010" "j $(hex target)"
    (let target (+ (& (pcd) 0xF0000000) (<< (u32 imm) 2)))
    (branch target))

(jtype JAL "000011" "jal $(hex target)"
    (let target (+ (& (pcd) 0xF0000000) (<< (u32 imm) 2)))
    (branch-with-link target))

(rtype JALR "001001" "jalr %$rd, %$rs"
    (let target (reg rs)
        (= (reg rd) (+ (pc) 8))
        (check-load-alignment target u32)
        (branch target)))
